plugins {
  id 'com.android.application'
  id 'org.jetbrains.kotlin.android'
  id 'kotlin-parcelize'
  id 'androidx.navigation.safeargs.kotlin'
  id "com.google.devtools.ksp"
  id 'com.google.protobuf'
}

android {
  compileSdk 32

  defaultConfig {
    applicationId "dev.goobar.androidstudyguide"
    minSdk 23
    targetSdk 32
    versionCode 1
    versionName "1.0"

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }

  defaultConfig {
    buildConfigField("String", "STUDY_GUIDE_SERVICE_URL", "${STUDY_GUIDE_SERVICE_URL}")
    buildConfigField("String", "GITHUB_ANDROID_WORKSHOP_TOKEN", GITHUB_ANDROID_WORKSHOP_TOKEN)
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  kotlinOptions {
    jvmTarget = '1.8'
  }
  buildFeatures {
    viewBinding = true
  }
}

/**
 * Sets up protobuf code generation so we can generate DataStore models from our protobuf messages
 */
protobuf {
  protoc {
    artifact = "com.google.protobuf:protoc:3.11.0"
  }

  generateProtoTasks {
    all().each { task ->
      task.builtins {
        java {
          option 'lite'
        }
      }
    }
  }
}

dependencies {

  implementation 'androidx.core:core-ktx:1.7.0'
  implementation 'androidx.appcompat:appcompat:1.4.2'
  implementation 'com.google.android.material:material:1.6.1'
  implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
  implementation 'androidx.navigation:navigation-fragment-ktx:2.5.0'
  implementation 'androidx.navigation:navigation-ui-ktx:2.5.0'

  // Used for network requests and json deserialization with Retrofit and Moshi
  implementation 'com.squareup.retrofit2:retrofit:2.9.0'
  implementation 'com.squareup.retrofit2:converter-moshi:2.9.0'
  implementation("com.squareup.moshi:moshi-kotlin:1.13.0")
  ksp("com.squareup.moshi:moshi-kotlin-codegen:1.13.0")

  // Volley will help us make network requests for uploading notes
  implementation("com.android.volley:volley:1.2.1")

  // used for DataStore
  implementation "androidx.datastore:datastore:1.0.0"
  implementation  "com.google.protobuf:protobuf-javalite:3.11.0"

  // used for Room database
  implementation("androidx.room:room-runtime:2.4.2")
  ksp("androidx.room:room-compiler:2.4.2")
  implementation("androidx.room:room-ktx:2.4.2")

  // WorkManager
  implementation "androidx.work:work-runtime-ktx:2.7.1"

  testImplementation 'junit:junit:4.13.2'
  testImplementation "androidx.arch.core:core-testing:2.1.0"
  testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.5.2'
  testImplementation "org.mockito.kotlin:mockito-kotlin:4.0.0"
  testImplementation 'app.cash.turbine:turbine:0.8.0'
  androidTestImplementation 'androidx.test.ext:junit:1.1.3'
  androidTestImplementation 'androidx.test:runner:1.4.0'
  androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}